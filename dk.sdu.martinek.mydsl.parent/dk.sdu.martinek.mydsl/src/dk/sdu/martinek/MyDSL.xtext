grammar dk.sdu.martinek.MyDSL with org.eclipse.xtext.common.Terminals

generate myDSL "http://www.sdu.dk/martinek/MyDSL"

Model:
	'package' name=QualifiedName ';'
	imports+=Import*
	
	headers=Headers?
	widgets=Widgets?
	entities=Entities?
	layouts=Layouts?
;

QualifiedName: ID ('.' ID)* ;
QualifiedNameWithWildcard: QualifiedName '.*'? ;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard ';';

Headers:
	{Headers} 'Headers:' headers+=STRING*
;

Widgets:
	{Widgets} 'Widgets:' widgets+=Widget*
;
	
Widget:
	'Widget' name=ID
	OPEN
		template=Template
		(properties+=Property)*	
	CLOSE
;

Template:
	'template' value=STRING
;

Property:
	'property' name=ID ('=' defaultValue=AttributeValue)?
;

Entities:
	{Specifications} 'Elements:' entities+=Entity*	
;

Entity:
	ref=[Widget|QualifiedName] name=ID (":" parent=[Entity|QualifiedName])?
	(
		OPEN
			attributes+=Attribute*
		CLOSE
	)?
;

Attribute:
	ref=[Property|QualifiedName] '=' right=AttributeValue
;

AttributeValue:
	  {MyStringValue}		val=STRING
	| {MyIntConstant}		val=INT
	| {MyBoolConstant}		val=('true' | 'false')
    | {MyEntityIdentifier}	ref=[Entity|QualifiedName]
;

Layouts:
	{Layouts} 'Layout:' layouts+=Layout*
;

Layout:
	ref=[Entity|QualifiedName]
	(
	OPEN
		childs+=Layout*
	CLOSE
	)?
;

    
terminal OPEN:
	'{'
;

terminal CLOSE:
	'}'
;
